plugins {
    id 'java'
    id 'eclipse'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.4'
    id("xyz.jpenilla.run-velocity") version "3.0.2"
}

import groovy.json.JsonSlurper

group = 'com.mcid'
// Read version from package.json
def packageJson = new JsonSlurper().parse(file('../package.json'))
version = packageJson.version

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    // Source: https://mvnrepository.com/artifact/tools.jackson.core/jackson-databind
    implementation 'tools.jackson.core:jackson-databind:3.0.4'
    // Source: https://mvnrepository.com/artifact/tools.jackson.core/jackson-core
    implementation 'tools.jackson.core:jackson-core:3.0.4'
    // Source: https://mvnrepository.com/artifact/dev.dejvokep/boosted-yaml
    implementation 'dev.dejvokep:boosted-yaml:1.3.7'
    compileOnly("com.velocitypowered:velocity-api:3.5.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.5.0-SNAPSHOT")
}

tasks {
    runVelocity {
        // Configure the Velocity version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        velocityVersion("3.5.0-SNAPSHOT")
    }
}
tasks.build {
    dependsOn shadowJar
}
def targetJavaVersion = 21
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
// Relocating a Package
shadowJar {
    relocate 'dev.dejvokep.boostedyaml', 'com.mcid.auth.libs'
}